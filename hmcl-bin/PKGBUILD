# Contributor: Rowisi < nomail <at> private <dot> com >
# Maintainer: So1ar <so1ar114514@gmail.com>

pkgname=hmcl-bin
_ver=3.5.4
_build=234
pkgver=${_ver}.${_build}
pkgrel=1
pkgdesc="A Minecraft Launcher which is multi-functional, cross-platform and popular."
arch=('any')
url="https://github.com/huanghongxun/HMCL"
license=('GPL3')
depends=('java17-openjfx' 'jre17-openjdk' 'gtk2')
provides=('hmcl')
conflicts=('hmcl')
source=("hmcl.desktop"
        "hmcl-launch-script"
        "craft_table.png"
        "LICENSE::https://raw.githubusercontent.com/huanghongxun/HMCL/javafx/LICENSE"
        "${pkgname}-${pkgver}-${pkgrel}.jar::https://github.com/huanghongxun/HMCL/releases/download/v${pkgver}/HMCL-${pkgver}.jar"
	"http://public.loongarch.dev/sources/minecraft/minecraft-jars.tar.xz/177a7da1b5ac9a4fbec7209a6168045e/minecraft-jars.tar.xz")
sha256sums=('9a561081f8f3ece3da114afd4f6d90565ca0e04716eef4ea88c6b4306566ae9b'
            'd7b373e705bfa287fa720a64820fa34c9df29ce87e73c1814c70e643434fe550'
            '2989a1b5301b8c7b9afdae5696c6a4e5246afa2d4f1f3d3dad5c192f036a9b4c'
            '6b0382b16279f26ff69014300541967a356a666eb0b91b422f6862f6b7dad17e'
            '4de4ae5abc2c8323a21b393a0ed7dccf8dced21fcae9fdb66e40295f0cb24cc5'
            '51b90ccdb6d9d548a4f70a25fa7d268db618332f6989157751ef5c2ef8f3f277')

noextract=("${pkgname}-${pkgver}-${pkgrel}.jar")
install=hmcl-bin.install

package() {
  install -Dm755 "hmcl-launch-script" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 "hmcl.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -Dm644 "${pkgname}-${pkgver}-${pkgrel}.jar" "${pkgdir}/usr/share/java/${pkgname}/${pkgname}.jar"
  install -Dm644 "craft_table.png" "${pkgdir}/usr/share/icons/hicolor/48x48/apps/${pkgname}.png"
  install -Dm644 "LICENSE" "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  mkdir -p "${pkgdir}/usr/share/java/lwjgl"
  cp ${srcdir}/minecraft-jars/*.jar "${pkgdir}/usr/share/java/lwjgl"
  cp ${srcdir}/minecraft-jars/install.sh "${pkgdir}/usr/share/java/lwjgl"
}
