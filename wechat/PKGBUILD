pkgname=wechat
pkgver=4.0.0.23
pkgrel=1
pkgdesc="wechat from Tencent(腾讯QQ)"
arch=('loong64')
url="https://weixin.qq.com"
license=('custom')
groups=('apps')
depends=(
    'liblol'
    'alsa-lib'
    'at-spi2-core'
    'bubblewrap'
    'flatpak-xdg-utils'
    'libxcomposite'
    'libxkbcommon-x11'
    'libxrandr'
    'lsb-release'
    'mesa'
    'nss'
    'openssl-1.1'
    'pango'
    'xcb-util-image'
    'xcb-util-keysyms'
    'xcb-util-renderutil'
    'xcb-util-wm'
    'xdg-desktop-portal'
    'xdg-user-dirs'
)
options=(strip !debug emptydirs)
makedepends=('dpkg')
install="${pkgname}.install"
source=("https://home-store-packages.uniontech.com/appstore/pool/appstore/c/com.tencent.wechat/wechat_${pkgver}_loongarch64.deb"
    "libtiff5.deb::https://pkg.loongnix.cn/loongnix/pool/main/t/tiff/libtiff5_4.1.0+git191117-2~deb10u1.lnd.4_loongarch64.deb"
    "libwebp6.deb::https://pkg.loongnix.cn/loongnix/pool/main/libw/libwebp/libwebp6_0.6.1-2.lnd.2_loongarch64.deb"
    "libjbig.deb::https://pkg.loongnix.cn/loongnix/pool/main/j/jbigkit/libjbig0_2.1-3.2_loongarch64.deb"
    "libjpeg.deb::https://pkg.loongnix.cn/loongnix/pool/main/libj/libjpeg-turbo/libjpeg62-turbo_1.5.2-2.lnd.4_loongarch64.deb"
    "license.tar.gz::https://aur.archlinux.org/cgit/aur.git/plain/license.tar.gz?h=wechat-universal-bwrap"
    "fake_dde-file-manager"
    "${pkgname}.sh"
    "${pkgname}.desktop"
)
sha256sums=('8bf5d8931e048285425f14ec9e765989fceb6584ecce20010db59916cbc8e2b4'
            '674d2fa5546480b32d72386b52fbd7b8fbda6c7a4162bd930527134da3eb74cf'
            'ca90e844566f0b86165164fc92d863155e99a7dedaf2c09d2cc1b129fcecb7b6'
            '5ca4f04ee553fd9bdf1090b766a0fd7b2474c1f72eb2d76dbc3f2183d243b0dc'
            'eb386d5b9dac62bebaf4d8ff6db7c269a2244f6a1c0c8224a3da7007c33f5e24'
            '53760079c1a5b58f2fa3d5effe1ed35239590b288841d812229ef4e55b2dbd69'
            'b25598b64964e4a38f8027b9e8b9a412c6c8d438a64f862d1b72550ac8c75164'
            'fe3fd94d8fdf01d992906121ecc1434806d33f047b5f583b23dbfd6447783bc2'
            '35f3cf67f92216c751eb6b2ef1135058077a214d3c03f5fcff862f09c85eba40')
noextract=()
for _source in "${source[@]}"; do
  if [[ "$_source" =~ ^lib.*deb$ ]]; then
    noextract+=("${_source%%::*}")
  fi
done

_pick_file() {
  local p="$1" f="$2" s="$3" d="$4" t
  t="$(echo "$p"|tr . _)"
  dpkg-deb -x ${srcdir}/${p} $t
  [ -d "${pkgdir}/$d" ] || mkdir -p "${pkgdir}/$d"
  cp -L "$t/$s/$f" "${pkgdir}/$d"
  rm -r $t
}

_pick_so() {
  local p="$1" f="$2" d="$3"
  _pick_file $p $f "/usr/lib/loongarch64-linux-gnu" $d
}

package() {
  echo 'Popupating pkgdir with data from wechat-universal deb file...'
  xz -cdT0 ./data.tar.xz | bsdtar -xpC "${pkgdir}" #./opt/wechat
#mv "${pkgdir}"/opt/{apps/com.tencent.wechat/files,"${pkgname}"}

  _pick_so libtiff5.deb libtiff.so.5 /opt/wechat/
  _pick_so libwebp6.deb libwebp.so.6 /opt/wechat/
  _pick_so libjbig.deb libjbig.so.0 /opt/wechat/
  _pick_so libjpeg.deb libjpeg.so.62 /opt/wechat/

# icons
#  echo 'Installing icons...'
#  for res in 16 32 48 64 128 256; do
#    install -Dm644 \
#      "${pkgdir}/opt/apps/com.tencent.wechat/entries/icons/hicolor/${res}x${res}/apps/com.tencent.wechat.png" \
#      "${pkgdir}/usr/share/icons/hicolor/${res}x${res}/apps/${pkgname}.png"
#  done
#  rm -rf "${pkgdir}"/opt/apps

  echo 'Fixing licenses...'
  local _wechat_root="${pkgdir}/usr/share/${pkgname}"
  install -dm755 {"${pkgdir}","${_wechat_root}"}/usr/lib/license
#  mv "${pkgdir}/opt/${pkgname}/libuosdevicea.so" "${_wechat_root}"/usr/lib/license/
  cp -ra license/etc "${_wechat_root}"
  cp -ra license/var "${_wechat_root}"

  echo 'Installing fake deepin file manager...'
  install -Dm755 {fake_,"${_wechat_root}"/usr/bin/}dde-file-manager

  echo 'Installing desktop files...'
  install -Dm644 "${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -Dm755 "${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
}
